import dev.kikugie.stonecutter.data.ParsedVersion
import java.io.File
import java.net.URI

plugins {
    id("net.fabricmc.fabric-loom-remap")

    // `maven-publish`
    // id("me.modmuss50.mod-publish-plugin")
    id("dev.kikugie.fletching-table.fabric") version "0.1.0-alpha.22"
}

version = "${property("mod.version")}+${sc.current.version}"
base.archivesName = property("mod.id") as String

val requiredJava = when {
    sc.current.parsed >= "1.20.6" -> JavaVersion.VERSION_21
    sc.current.parsed >= "1.18" -> JavaVersion.VERSION_17
    sc.current.parsed >= "1.17" -> JavaVersion.VERSION_16
    else -> JavaVersion.VERSION_1_8
}

repositories {
    /**
     * Restricts dependency search of the given [groups] to the [maven URL][url],
     * improving the setup speed.
     */
    fun strictMaven(url: String, alias: String, vararg groups: String) = exclusiveContent {
        forRepository { maven(url) { name = alias } }
        filter { groups.forEach(::includeGroup) }
    }
    strictMaven("https://www.cursemaven.com", "CurseForge", "curse.maven")
    strictMaven("https://api.modrinth.com/maven", "Modrinth", "maven.modrinth")

    maven {
        name = "Terraformers"
        url = URI.create("https://maven.terraformersmc.com/")
    }

    maven {
        name = "Xander Maven"
        url = URI.create("https://maven.isxander.dev/releases")
    }

    maven {
        name = "UkuLib Maven"
        url = URI.create("https://maven.uku3lig.net/releases")
    }

    maven {
        name = "Jitpack"
        url = URI.create("https://jitpack.io")
    }
}

dependencies {
    // minecraft things
    minecraft("com.mojang:minecraft:${sc.current.version}")
    mappings(loom.officialMojangMappings())
    modImplementation("net.fabricmc:fabric-loader:${property("deps.fabric_loader")}")

    // mod dependencies / integrations
    modImplementation("net.uku3lig:ukulib:${property("deps.ukulib")}")
    modImplementation("net.kyori:adventure-platform-fabric:${property("deps.adventure")}")
    modImplementation("net.fabricmc.fabric-api:fabric-api:${property("deps.fabric_api")}")

    // bundled dependencies & libraries
    include("net.kyori:adventure-platform-fabric:${property("deps.adventure")}")
    compileOnly("org.projectlombok:lombok:1.18.42")
    annotationProcessor("org.projectlombok:lombok:1.18.42")

    // mod integrations
    modApi(fletchingTable.modrinth("modmenu", property("mod.mc_dep") as String, "fabric"))
    modApi(fletchingTable.modrinth("tiertagger", property("mod.mc_dep") as String, "fabric"))
}

loom {
    fabricModJsonPath = rootProject.file("src/main/resources/fabric.mod.json") // Useful for interface injection
    accessWidenerPath = rootProject.file("src/main/resources/template.accesswidener")

    decompilerOptions.named("vineflower") {
        options.put("mark-corresponding-synthetics", "1") // Adds names to lambdas - useful for mixins
    }

    runConfigs.all {
        ideConfigGenerated(true)
        vmArgs("-Dmixin.debug.export=true") // Exports transformed classes for debugging
        runDir = "../../run" // Shares the run directory between versions
    }
}

java {
    withSourcesJar()
    targetCompatibility = requiredJava
    sourceCompatibility = requiredJava
}

val generatedConfigContainerIndexDir = layout.buildDirectory.dir("generated/sources/configContainerIndex/java/main")

sourceSets.named("main") {
    java.srcDir(generatedConfigContainerIndexDir)
}

tasks {
    register("generateConfigContainerIndex") {
        group = "build"
        description = "Generates config container index from @Configurable* declarations"

        val sourceRoot = rootProject.file("src/main/java")
        val outputRoot = generatedConfigContainerIndexDir
        val outputFile = outputRoot.map {
            File(it.asFile, "dev/candycup/lifestealutils/config/generated/GeneratedConfigContainerIndex.java")
        }

        inputs.dir(sourceRoot)
        outputs.file(outputFile)

        doLast {
            val configurableRegex = Regex("@Configurable(Boolean|String|Minimessage|Float|Enum|List|ToggleGroup)\\b")
            val packageRegex = Regex("(?m)^\\s*package\\s+([a-zA-Z0-9_.]+)\\s*;")
            val classRegex = Regex("\\b(public\\s+)?(final\\s+)?(abstract\\s+)?(class|enum|interface|record)\\s+([A-Za-z_][A-Za-z0-9_]*)")
            val discoveredClasses = linkedSetOf<String>()

            sourceRoot.walkTopDown()
                .filter { it.isFile && it.extension == "java" }
                .forEach { javaFile ->
                    val source = javaFile.readText()
                    if (!configurableRegex.containsMatchIn(source)) {
                        return@forEach
                    }

                    val packageName = packageRegex.find(source)?.groupValues?.get(1) ?: return@forEach
                    val className = classRegex.find(source)?.groupValues?.get(5) ?: return@forEach
                    discoveredClasses.add("$packageName.$className")
                }

            val sortedClasses = discoveredClasses.toList().sorted()
            val output = outputFile.get()
            output.parentFile.mkdirs()

            val content = buildString {
                appendLine("package dev.candycup.lifestealutils.config.generated;")
                appendLine()
                appendLine("import dev.candycup.lifestealutils.config.ConfigContainerRegistry;")
                appendLine()
                appendLine("public final class GeneratedConfigContainerIndex {")
                appendLine("   private GeneratedConfigContainerIndex() {")
                appendLine("   }")
                appendLine()
                appendLine("   public static void registerAll() {")
                appendLine("      ConfigContainerRegistry.clear();")
                sortedClasses.forEach { fqcn ->
                    appendLine("      ConfigContainerRegistry.registerContainer($fqcn.class);")
                }
                appendLine("   }")
                appendLine("}")
            }

            output.writeText(content)
        }
    }

    named("compileJava") {
        dependsOn("generateConfigContainerIndex")
    }

    named("sourcesJar") {
        dependsOn("generateConfigContainerIndex")
    }

    processResources {
        inputs.property("id", project.property("mod.id"))
        inputs.property("name", project.property("mod.name"))
        inputs.property("version", project.property("mod.version"))
        inputs.property("minecraft", project.property("mod.mc_dep"))

        val props = mapOf(
            "id" to project.property("mod.id"),
            "name" to project.property("mod.name"),
            "version" to project.property("mod.version"),
            "minecraft" to project.property("mod.mc_dep")
        )

        filesMatching("fabric.mod.json") { expand(props) }

        val mixinJava = "JAVA_${requiredJava.majorVersion}"
        filesMatching("*.mixins.json") { expand("java" to mixinJava) }
    }

    // Builds the version into a shared folder in `build/libs/${mod version}/`
    register<Copy>("buildAndCollect") {
        group = "build"
        from(remapJar.map { it.archiveFile }, remapSourcesJar.map { it.archiveFile })
        into(rootProject.layout.buildDirectory.file("libs/${project.property("mod.version")}"))
        dependsOn("build")
    }
}

/*
// Publishes builds to Modrinth and Curseforge with changelog from the CHANGELOG.md file
publishMods {
    file = tasks.remapJar.map { it.archiveFile.get() }
    additionalFiles.from(tasks.remapSourcesJar.map { it.archiveFile.get() })
    displayName = "${property("mod.name")} ${property("mod.version")} for ${property("mod.mc_title")}"
    version = property("mod.version") as String
    changelog = rootProject.file("CHANGELOG.md").readText()
    type = STABLE
    modLoaders.add("fabric")

    dryRun = providers.environmentVariable("MODRINTH_TOKEN").getOrNull() == null
        || providers.environmentVariable("CURSEFORGE_TOKEN").getOrNull() == null

    modrinth {
        projectId = property("publish.modrinth") as String
        accessToken = providers.environmentVariable("MODRINTH_TOKEN")
        minecraftVersions.addAll(property("mod.mc_targets").toString().split(' '))
        requires {
            slug = "fabric-api"
        }
    }

    curseforge {
        projectId = property("publish.curseforge") as String
        accessToken = providers.environmentVariable("CURSEFORGE_TOKEN")
        minecraftVersions.addAll(property("mod.mc_targets").toString().split(' '))
        requires {
            slug = "fabric-api"
        }
    }
}
 */
/*
// Publishes builds to a maven repository under `com.example:template:0.1.0+mc`
publishing {
    repositories {
        maven("https://maven.example.com/releases") {
            name = "myMaven"
            // To authenticate, create `myMavenUsername` and `myMavenPassword` properties in your Gradle home properties.
            // See https://stonecutter.kikugie.dev/wiki/tips/properties#defining-properties
            credentials(PasswordCredentials::class.java)
            authentication {
                create<BasicAuthentication>("basic")
            }
        }
    }

    publications {
        create<MavenPublication>("mavenJava") {
            groupId = "${property("mod.group")}.${property("mod.id")}"
            artifactId = property("mod.id") as String
            version = project.version

            from(components["java"])
        }
    }
}
 */
